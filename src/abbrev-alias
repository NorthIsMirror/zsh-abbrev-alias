#!/usr/bin/env zsh

typeset -g _ABBREV_ALIAS_VERSION='0.2.0'

typeset -Ag _abbrev_aliases

__abbrev_alias::magic_abbrev_expand() {
  local MATCH
  LBUFFER=${LBUFFER%%(#m)[-_a-zA-Z0-9]#}
  local abbr=${_abbrev_aliases[$MATCH]:-  $MATCH}
  local kind=${abbr[1,1]}
  local is_eval=${abbr[2,2]}
  local newbuffer=${abbr[3,-1]}
  if [[ $kind == "c" && $LBUFFER != "" ]]; then
    newbuffer=$MATCH
  fi
  if [[ "$is_eval" == "1" ]]; then
    newbuffer=$(eval "echo \"$newbuffer\"")
  fi
  LBUFFER+=$newbuffer
  zle self-insert
}

__abbrev_alias::no_magic_abbrev_expand() {
  LBUFFER+=' '
}

__abbrev_alias::init() {
  setopt extended_glob
  zle -N __abbrev_alias::magic_abbrev_expand
  zle -N __abbrev_alias::no_magic_abbrev_expand
  bindkey " "   __abbrev_alias::magic_abbrev_expand
  bindkey "^x " __abbrev_alias::no_magic_abbrev_expand
}

__abbrev_alias::show() {
  local kind_filter=$1
  local key=$2
  local alias_data=${_abbrev_aliases[$key]}
  test -n $alias_data || return 1
  local kind=${alias_data[1,1]}
  local value=${alias_data[3,-1]}
  if [[ $kind_filter == $kind ]]; then
    echo "$key='$value'"
  fi
}

__abbrev_alias::list() {
  local kind_filters=$1
  for kind_filter in ${(s::)kind_filters}; do
    for key in ${(ko)_abbrev_aliases}; do
      __abbrev_alias::show $kind_filter $key
    done
  done
}

__abbrev_alias::version() {
  echo "abbrev-alias ${_ABBREV_ALIAS_VERSION}"
}

__abbrev_alias::help() {
  __abbrev_alias::version
  echo "usage: abbrev-alias [OPTIONS] {name=value ...}"
  echo "       abbrev-alias -u {name ...}"
  echo "       abbrev-alias --init"
  echo
  echo "options:"
  echo "  -c, --command   register alias as 'alias name=value'"
  echo "  -g, --global    register alias as 'alias -g name=value'"
  echo "  -e, --eval      evaluates subshells on expansion. "
  echo "  -u, --unset     unregister alias"
  echo "  -i, --init      initialize abbrev-alias. execute with .zshrc"
  echo "  -h, --help      show this help"
  echo "  -v, --version   show version"
}

__abbrev_alias::unregist() {
  local key=$1
  if [[ -n "${_abbrev_aliases[$key]}" ]]; then
    unalias $key
    unset "_abbrev_aliases[$key]"
  else
    echo "no such alias: $key" >&2
    return 1
  fi
}

__abbrev_alias::regist() {
  local kind=$1 is_eval=$2 key=${3%%=*} value=${3#*=}
  if [[ "$kind" == "f" ]]; then
    echo "WARN: -f option is deprecated. Use -g -e options." 1>&2
    echo "      ex) abbrev-alias -g -e $key='\$($value)'" 1>&2
    value="\$($value)"
    is_eval=1
    kind=g
  fi
  case "$kind" in
    g) alias -g $key="$value";;
    c) alias    $key="$value";;
  esac
  _abbrev_aliases[$key]="$kind$is_eval$value"
}

# option parse & execute
zparseopts -D -M -A opts -- \
  -init i=-init \
  -help h=-help \
  -version v=-version \
  -unset u=-unset \
  -command c=-command \
  -global g=-global \
  -function f=-function \
  -eval e=-eval

# init
if [[ -n ${opts[(i)--init]} ]]; then
  __abbrev_alias::init
  return
fi

# help
if [[ -n ${opts[(i)--help]} ]]; then
  __abbrev_alias::help
  return 0
fi

# version
if [[ -n ${opts[(i)--version]} ]]; then
  __abbrev_alias::version
  return 0
fi

# unregist
if [[ -n ${opts[(i)--unset]} ]]; then
  local result=0
  while [ $# -gt 0 ]; do
    __abbrev_alias::unregist "$1"
    [ $? -ne 0 ] && result=1
    shift
  done
  return $result
fi

# set options
local kind
if [[ -n ${opts[(i)--command]} ]]; then
  kind=c
elif [[ -n ${opts[(i)--global]} ]]; then
  kind=g
elif [[ -n ${opts[(i)--function]} ]]; then
  kind=f
fi 
[[ ! -n ${opts[(i)--eval]} ]]; local is_eval=$?

#list
if [ $# -eq 0 ]; then
  __abbrev_alias::list ${kind:-gc}
  return 0
fi

# regist
while [ $# -gt 0 ]; do
  __abbrev_alias::regist ${kind:-c} $is_eval "$1"
  shift
done
return 0
